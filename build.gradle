import java.text.SimpleDateFormat

buildscript {
    ext.kotlin_version = '1.3.+'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name "papeeria-maven"
            url "gcs://papeeria-maven-repo/maven2"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.+'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm"
    id 'com.google.cloud.tools.jib'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    libprotoImplementation
}

dependencies {
    libprotoImplementation 'com.google.protobuf:protobuf-java:3.+'
    libprotoImplementation 'io.grpc:grpc-protobuf:1.19.+'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'io.grpc:grpc-stub:1.+'
    compile "io.grpc:grpc-netty:1.+"
    compile "io.grpc:grpc-testing:1.+"
    compile "com.xenomachina:kotlin-argparser:2.0.+"
    compile 'com.google.cloud:google-cloud-storage:1.23.+'
    compile 'com.google.cloud:google-cloud-datastore:1.23.+'
    compile 'com.google.cloud:google-cloud-nio:0.42.+'
    compile 'com.google.guava:guava:27.1-jre'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.3.+'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.3.+'
    compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: '2.0.+'
    compile configurations.libprotoImplementation

    testCompile "org.junit.jupiter:junit-jupiter-engine:5.+"
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.+'
}

dependencyLocking {
    lockAllConfigurations()
}

sourceSets {
    main {
        proto {
        }
        java {
        }
    }
    libproto {
        java {
            srcDirs "$projectDir/src/generated/main/java"
        }
    }
}

sourceCompatibility = 1.8

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.+'
        generatedFilesBaseDir = "$projectDir/src/generated"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.+'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

task libproto(type: Jar) {
    dependsOn = ["compileLibprotoJava"]
    from sourceSets.libproto.output.classesDirs
    include "com/bardsoftware/**"
    archiveName = "cosmas-proto-${version}.jar"
}

mainClassName = 'com.bardsoftware.papeeria.backend.cosmas.CosmasServerKt'

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

jib {
    from {
        image = 'gcr.io/distroless/java:debug'
    }
    extraDirectory {
        permissions = [
                '/var/log/cosmas': '777',
                '/var/log/cosmas/cosmas.log': '666'
        ]
    }
}

group 'com.bardsoftware.papeeria.backend'
version new SimpleDateFormat("yyyy.MM.dd").format(new Date())

publishing {
    repositories {
        maven {
            name "papeeria-maven"
            url "gcs://papeeria-maven-repo/maven2"
        }

    }
    publications {
        mavenCosmasProto(MavenPublication) {
            artifactId = 'cosmas-proto'
            artifact libproto
            version project.version
        }
    }
}
